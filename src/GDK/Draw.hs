{-# LANGUAGE TypeFamilies #-}
{-# LANGUAGE RankNTypes #-}
{-# LANGUAGE FlexibleContexts #-}

module GDK.Draw (draw, drawTexture, drawText) where

import qualified SDL
import qualified SDL.Font as TTF
import Apecs
import GDK.Types
import GDK.Texture
import GDK.Font (RenText(..), FontMap(..))
import Control.Monad.IO.Class (MonadIO)
import qualified Data.Vector as V
import qualified Data.Text as T
import qualified Data.Map as Map
import Data.Maybe (fromMaybe)

{-|
Draw all 'Renderable' entities onto their appropriate layer.
This function is the typical draw function to pass to 'run', however you are free to implement your own
-}
draw :: forall w.
      (Has w IO TextureMap
      , Get w IO Window
      , Get w IO Renderable
      , Get w IO Position
      , Get w IO FontMap)
      => SDL.Renderer
      -> FPS
      -> System w ()
draw renderer fps = do
    Window win <- get global
    let window = fromMaybe (error "Window not initialised") win
    size <- SDL.get $ SDL.windowSize window
    maxLayer <- cfold (\acc r -> case r of
        RenderableTexture t -> max acc (GDK.Texture.layer t)
        RenderableText t -> max acc (GDK.Font.layer t)) 0
    layers <- V.replicateM maxLayer (SDL.createTexture renderer SDL.RGBA8888 SDL.TextureAccessTarget size)
    TextureMap tm <- get global
    FontMap fm <- get global
    cmapM_ $ \(r, pos) -> case r of
        RenderableTexture t -> case Map.lookup (textureRef t) tm of
            Just td -> do
                let layerTex = layers V.! GDK.Texture.layer t
                SDL.rendererRenderTarget renderer SDL.$= Just layerTex
                drawTexture renderer td pos (animationFrame t)
            Nothing -> return () -- Texture not found, skip drawing
        RenderableText t -> case Map.lookup (fontRef t) fm of
            Just font -> do
                let layerTex = layers V.! GDK.Font.layer t
                SDL.rendererRenderTarget renderer SDL.$= Just layerTex
                drawText renderer t font pos
            Nothing -> return () -- Font not found, skip drawing
    SDL.rendererRenderTarget renderer SDL.$= Nothing
    V.mapM_ (\t -> do
        SDL.copy renderer t Nothing Nothing
        SDL.destroyTexture t) layers

{-|
Draw a 'Texture' given its 'TextureData' and 'Position'
If either the 'TextureData' does not have an 'Animation' or no frame index is provided, the texture will be drawn as if it was static.
-}
drawTexture :: SDL.Renderer -> TextureData -> Position -> Maybe Int -> System w ()
drawTexture r (TextureData t (Just a)) (Position pos) (Just n) = do
    info <- liftIO $ SDL.queryTexture t
    let w = fromIntegral $ SDL.textureWidth info
        h = fromIntegral $ SDL.textureHeight info
        fw = w `div` fromIntegral (frameCount a)
        srcRect = SDL.Rectangle (SDL.P (SDL.V2 (fromIntegral n * fw) 0)) (SDL.V2 fw h)
        dstRect = SDL.Rectangle (SDL.P (round <$> pos)) (SDL.V2 fw h)
    liftIO $ SDL.copy r t (Just srcRect) (Just dstRect)
drawTexture r (TextureData t _) (Position pos) _ = do
    info <- liftIO $ SDL.queryTexture t
    let w = fromIntegral $ SDL.textureWidth info
        h = fromIntegral $ SDL.textureHeight info
        pos' = SDL.Rectangle (SDL.P (round <$> pos)) (SDL.V2 w h)
    liftIO $ SDL.copy r t Nothing (Just pos')

-- | Draw text given its 'RenText', 'Font' and 'Position'
drawText :: SDL.Renderer -> RenText -> TTF.Font -> Position -> System w ()
drawText r t font (Position pos) = do
    (tex, size) <- generateSolidText r font (colour t) (text t)
    SDL.copy r tex Nothing (Just $ SDL.Rectangle (SDL.P (round <$> pos)) (fromIntegral <$> size))
    SDL.destroyTexture tex

generateSolidText :: MonadIO m => SDL.Renderer -> TTF.Font -> TTF.Color -> String -> m (SDL.Texture, SDL.V2 Int)
generateSolidText r font = generateText r font (TTF.solid font)

generateText :: MonadIO m => SDL.Renderer -> TTF.Font -> (TTF.Color -> T.Text -> m SDL.Surface) -> TTF.Color -> String -> m (SDL.Texture, SDL.V2 Int)
generateText r font f col str = do
    let t = T.pack str
    surface <- f col t
    tex <- liftIO $ SDL.createTextureFromSurface r surface
    SDL.freeSurface surface
    (w,h) <- liftIO $ TTF.size font t
    return (tex, SDL.V2 w h)

{-|
A global component which stores in a Vector a 'Texture' for each render layer.
The index of the vector corresponds to the render layer, so the texture at index 0 is the texture for render layer 0, and so on.
Lower layers are drawn first, and therefore can appear behind higher layers.
The texture for each layer is cleared at the start of each frame, and 'Renderables' are drawn onto the appropriate layer texture during the draw phase of the game loop.
-}
-- newtype RenderLayers = RenderLayers (V.Vector SDL.Texture)
-- instance Semigroup RenderLayers where
--     (RenderLayers q1) <> (RenderLayers q2) = RenderLayers (q1 V.++ q2)
-- instance Monoid RenderLayers where
--     mempty = RenderLayers V.empty
-- instance Component RenderLayers where type Storage RenderLayers = Global RenderLayers